plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

sourceCompatibility = 17
targetCompatibility = 17

archivesBaseName = "crowdin-translate"
version = "1.4+22w05a"

loom {
    mixin.defaultRefmapName = "crowdin-translate-refmap.json";
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// These are, actually, not even used in this project.
// They are only included to make loom happy.
// Use old versions intentionally to make this work with a broad range of MCs.
dependencies {
    minecraft "com.mojang:minecraft:22w05a"
    mappings "net.fabricmc:yarn:22w05a+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:0.13.1"
}


// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
java {
    withSourcesJar()
}

jar {
    from "LICENSE"
    manifest {
        attributes(
            'Main-Class': 'de.guntram.mcmod.crowdintranslate.CrowdinTranslate'
        )
    }
}

gradlePlugin {
    plugins {
        gradleplugin {
            id = 'de.guntram.mcmod.crowdin-translate'
            implementationClass = 'de.guntram.mcmod.crowdintranslate.GradlePlugin.CrowdinTranslatePlugin'
        }
    }
}

group = "de.guntram.mcmod"
// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

task mypublish(dependsOn: publish, type: Exec) {
    commandLine "rsync", "-av", "/tmp/mymavenrepo/", "maven@minecraft.guntram.de:/var/www/html/maven/"
}
